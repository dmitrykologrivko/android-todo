apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.dmitrykologrivkogmail.todolist"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.dmitrykologrivkogmail.todolist.MockTestRunner"
        //com.android.ddmlib.DdmPreferences.setTimeOut(60000)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }
}

dependencies {
    final APPCOMPAT_VERSION = '25.3.1'
    final DESIGN_VERSION = '25.3.1'
    final CONSTRAINT_LAYOUT_VERSION = '1.0.1'
    final SECURE_PREFERENCES_VERSION = '0.1.4'
    final RETROFIT_VERSION = '2.1.0'
    final MOSBY_VERSION = '2.0.1'
    final RXJAVA_VERSION = '1.2.5'
    final RXANDROID_VERSION = '1.2.1'
    final DAGGER_VERSION = '2.9'
    final BUTTERKNIFE_VERSION = '8.5.1'
    final STETHO_VERSION = '1.4.2'
    final ESPRESSO_VERSION = '2.2.2'
    final JUNIT_VERSION = '4.12'
    final MOCKITO_VERSION = '2.7.22'
    final DEXMAKER_VERSION = '2.2.0'
    final ROBOLECTRIC_VERSION = '3.3.2'
    final MOCKWEBSERVER_VERSION = '20130706'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    //////////////////////////////////////////////////////////////////////////////////////////////
    //                                 App Dependencies                                         //
    //////////////////////////////////////////////////////////////////////////////////////////////

    /* Support */
    compile "com.android.support:appcompat-v7:$APPCOMPAT_VERSION"
    compile "com.android.support:recyclerview-v7:$APPCOMPAT_VERSION"
    compile "com.android.support:design:$DESIGN_VERSION"
    compile "com.android.support.constraint:constraint-layout:$CONSTRAINT_LAYOUT_VERSION"

    /* Secure preferences */
    compile "com.scottyab:secure-preferences-lib:$SECURE_PREFERENCES_VERSION"

    /* Network */
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"

    /* MVP */
    compile "com.hannesdorfmann.mosby:mvp:$MOSBY_VERSION"

    /* ReactiveX */
    compile "io.reactivex:rxjava:$RXJAVA_VERSION"
    compile "io.reactivex:rxandroid:$RXANDROID_VERSION"

    /* Dependency injection */
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    /* View binding */
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    /* Debug */
    compile "com.facebook.stetho:stetho:$STETHO_VERSION"
    compile "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"

    //////////////////////////////////////////////////////////////////////////////////////////////
    //                            Instrumentation test dependencies                             //
    //////////////////////////////////////////////////////////////////////////////////////////////

    androidTestCompile("com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("com.android.support.test.espresso:espresso-intents:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestCompile "com.linkedin.dexmaker:dexmaker-mockito:$DEXMAKER_VERSION"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    //////////////////////////////////////////////////////////////////////////////////////////////
    //                                  Unit tests dependencies                                 //
    //////////////////////////////////////////////////////////////////////////////////////////////

    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile "org.robolectric:robolectric:$ROBOLECTRIC_VERSION"
    testCompile "com.google.mockwebserver:mockwebserver:$MOCKWEBSERVER_VERSION"
    testCompile ("com.google.dagger:dagger-compiler:$DAGGER_VERSION", {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}
